월별 무역 수출액 자료 : 
등분산 의심 -> Box-Cox 변환 -> 비정상 시계열 -> 일반 차분 -> 평균이 0인 ARIMA(0,1,1)(3,0,0)12(0,0,1)10 모형 ???

sas code

/* Time series analysis */

/* 데이터 생성 */
proc import out=trade
datafile='D:\수업자료\(3)Junior\3-1\시계열분석및실습(1전공)\Team project\자료\월별 무역 수출액 (1996.01~2012.12) (단위 천달러).csv'
dbms=csv replace ;
run ;

data trade ;
set trade ;
t+1 ;
time=intnx('month','01jan96'd,_N_-1) ;
format time monyy. ;
if 205<=_N_<=215 then delete ;
run ;

proc print data=trade ;
run ;

/* 그래프 그리기 */
symbol1 v=none i=join l=1 ;
symbol2 v=none i=join l=2 ;
symbol3 v=none i=join l=3 ;
symbol4 v=none i=join l=4 ;
proc gplot data=trade ;
plot z*time ;
run ; quit ;

/* 등분산성 의심 -> Box-Cox 변환 */
ods graphics on ;
proc transreg data=trade detail ;
model boxcox(z / convenient lambda=-2 to 2 by 0.01)=identity(t) ;
run ; quit ;
ods graphics off ;

/* lambda=0 -> log transformation */
data trade ; 
set trade ;
w=log(z) ;
run ;
proc gplot data=trade ;
plot w*time ;
run ; quit ;

/* 모형의 식별 및 모수의 추정 */
proc arima data=trade ;
identify var=w stationarity=(adf=(1,12)) ;
run ; quit ;

/* 비정상 시계열 -> 일반 차분 실시 */
data trade ;
set trade ;
y=dif(w) ;
run ;
proc gplot data=trade ; /* Yt=(1-B)*Wt */
plot y*t ;
run ; quit ;
proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
run ; quit ;

/* 잠정모형찾기 */
/* Yt -> AR(1)12 ?? */
proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
estimate p=(12) plot ;
run ; quit ;

/* Yt -> AR(3)12 ?? */
proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
estimate p=(12,24,36) plot ;
run ; quit ;

proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
estimate p=(12,36) noconstant plot ;
run ; quit ;

/* Yt -> AR(1)(3)12 ?? */
proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
estimate p=(1)(12,36) noconstant plot ;
run ; quit ;

/* Yt -> ARMA(1,0)(3,0)12(0,1)10 ?? */
proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
estimate p=(1)(12,36) q=(10) noconstant plot ;
run ; quit ;

/* Yt -> ARMA(1,1)(3,0)12(0,1)10 ?? */
proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
estimate p=(1)(12,36) q=(1)(10) noconstant plot ;
run ; quit ;

/* Yt -> ARMA(0,1)(3,0)12(0,1)10 ?? */
proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
estimate p=(12,36) q=(1)(10) noconstant plot ;
run ; quit ;

/* Yt -> ARMA(0,6)(3,0)12(0,1)10 ?? */
proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
estimate p=(12,36) q=(1,6)(10) noconstant plot ;
run ; quit ;

/* Yt -> ARMA(0,28)(3,0)12(0,1)10 ?? */
proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
estimate p=(12,36) q=(1,6,28)(10) noconstant plot ;
run ; quit ;

/* 최종모형선택 및 예측 (SBC 기준 : ARIMA(0,1,1)(3,0,0)12(0,0,1)10) */
/* (1-B)*(1-0.038509*B**12-0.27295*B**36)*Wt=(1-0.33262*B)*(1-0.2023*B**10)*at */
ods graphics on ;
proc arima data=trade ;
identify var=w(1) stationarity=(adf=(1,12)) nlag=36 ;
estimate p=(12,36) q=(1)(10) noconstant plot ;
forecast id=t lead=36 out=result ;
run ; quit ;
ods graphics off ;
proc print data=result ;
run ;

/* Final graph (원래의 데이터인 z의 기준으로 바꾸고 그래프 그리기) */
/* (1-B)*(1-0.038509*B**12-0.27295*B**36)*log(Zt)=(1-0.33262*B)*(1-0.2023*B**10)*at */
data origin ;
set result ;
time=intnx('month','01jan96'd,_N_-1) ;
format time date7. ;
z=exp(w) ; forecast_z=exp(forecast) ; l95_z=exp(l95) ; u95_z=exp(u95) ;
residual_z=z-forecast_z ;
drop w forecast std l95 u95 residual ;
run ;
proc print data=origin ;
run ;

proc gplot data=origin ;
plot (z forecast_z)*time / overlay ;
run ; quit ;
proc gplot data=origin ;
plot (z forecast_z l95_z u95_z)*time / overlay ;
run ; quit ;


